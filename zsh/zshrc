# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export ZSH="$ZDOTDIR"

load_prompt_theme () {
    [[ -n "${1+set}" ]] || return 1

    local theme="$1"
    . "$ZSH/themes/$theme/$theme.zsh-theme"
}

load_plugins () {
    local repo
    for repo in $(grep -Ev '^#|^$' < "$ZSH/plugins.zsh"); do
        local plugin=$(basename $repo)
        . "$ZSH/plugins/$plugin/$plugin.plugin.zsh"
    done
}

# Load common rc
[[ ! -f ~/.config/sh/shrc ]] || . ~/.config/sh/shrc

precmd () { set_terminal_title zsh - $(basename "${PWD/$HOME/~}"); }

HISTFILE="$ZSH/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000
setopt appendhistory
# Don't save duplicates on history
setopt HIST_IGNORE_ALL_DUPS
# Don't show duplicates on history search
setopt HIST_FIND_NO_DUPS

# Case-insensitive completion
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'

# Enable history substring search with c-p, c-n
bindkey '^P' history-beginning-search-backward
bindkey '^N' history-beginning-search-forward

# Enable quotes text objects in vi-mode
# https://github.com/ohmyzsh/ohmyzsh/issues/5708
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
    for c in {a,i}{\',\",\`}; do
        bindkey -M $m $c select-quoted
    done
done

load_prompt_theme powerlevel10k
load_plugins

# Fix cursor in visual mode
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_HIGHLIGHTERS+=(cursor)
ZSH_HIGHLIGHT_STYLES[cursor]=bg=white

# Load tab completions (should stay at the end)
autoload -Uz compinit && compinit

# To customize prompt, run `p10k configure` or edit .p10k.zsh.
[[ ! -f "$ZSH/.p10k.zsh" ]] || . "$ZSH/.p10k.zsh"
